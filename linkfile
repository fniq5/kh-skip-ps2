/*
  Modified for kh-skip-ps2

	Copyright 2001-2004, ps2dev - http://www.ps2dev.org
	Licenced under Academic Free License version 2.0
	Review ps2sdk README & LICENSE files for further details.

	Standard linkfile
*/

ENTRY(_start);

PHDRS {
  text PT_LOAD;
}


SECTIONS {
	.text 0x84000 : {
		*(.text)
	} :text

	.reginfo : { *(.reginfo) } 

	.ctors ALIGN(16): {
		KEEP(*crtbegin*.o(.ctors))
		KEEP(*(EXCLUDE_FILE(*crtend*.o) .ctors))
		KEEP(*(SORT(.ctors.*)))
		KEEP(*(.ctors))
	} 
	.dtors ALIGN(16): {
		KEEP(*crtbegin*.o(.dtors))
		KEEP(*(EXCLUDE_FILE(*crtend*.o) .dtors))
		KEEP(*(SORT(.dtors.*)))
		KEEP(*(.dtors))
	} 

	.rodata ALIGN(128): {
		*(.rodata)
	} 

	.data ALIGN(128): {
		_fdata = . ;
		*(.data)
		SORT(CONSTRUCTORS)
	} 

	_gp = ALIGN(128) + 0x7ff0;
	.lit4 ALIGN(128): { *(.lit4) } 
	.lit8 ALIGN(128): { *(.lit8) } 

	.sdata ALIGN(128): {
		*(.sdata)
	} 

	.sbss ALIGN(128) : {
		_fbss = . ;
		*(.sbss)
	} 

	.bss ALIGN(128) : {
		*(.bss)
	} 

	/* Symbols needed by crt0.s.  */
	PROVIDE(_heap_size = -1);
	PROVIDE(_stack = ALIGN(16));
	PROVIDE(_stack_size = 0x1000);
        PROVIDE(_end = _stack + _stack_size);
}
